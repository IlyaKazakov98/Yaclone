apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.readyfo.yaclone"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha06'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    moxy()
    retrofit()
    room()
    rxJava()
    navigation()
    dagger()
    mockito()
}

def moxy() {
    dependencies {
        def moxyVersion = '2.1.2'

        implementation "com.github.moxy-community:moxy:$moxyVersion"
        kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"
        implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
    }
}

def retrofit() {
    dependencies {
        def retrofitVersion = "2.6.0"

        implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
        implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
        implementation "com.squareup.retrofit2:adapter-rxjava2:2.6.3"
        implementation 'com.squareup.okhttp3:logging-interceptor:4.2.0'
    }
}

def room() {
    dependencies {
        def roomVersion = "2.2.5"

        implementation "androidx.room:room-runtime:$roomVersion"
        implementation "androidx.room:room-ktx:$roomVersion"
        implementation "androidx.room:room-rxjava2:$roomVersion"
        kapt "androidx.room:room-compiler:$roomVersion"
    }
}

def rxJava() {
    dependencies {
        def rxJava = "2.2.19"
        def rxAndroid = "2.1.1"

        implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
        implementation "io.reactivex.rxjava2:rxjava:$rxJava"
    }
}

def navigation() {
    dependencies {
        def navigationVersion = "2.3.0"

        implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
        implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    }
}

def dagger() {
    dependencies {
        def daggerVersion = "2.27"

        api "com.google.dagger:dagger:$daggerVersion"
        kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    }
}

def mockito() {
    dependencies {
        def mockitoVersion = "2.2.0"

        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoVersion"
    }
}
